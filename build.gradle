plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id "com.diffplug.gradle.spotless" version "3.18.0"
} 

allprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"

    repositories {
        jcenter()
    }

}

subprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.diffplug.gradle.spotless"
    
    spotless {
        kotlin {
            ktlint()
        }
    }

    repositories {
        jcenter()
        maven {
            url "https://repo.spring.io/milestone/"
        }
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.2.RELEASE'
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

task copyAppDependencies(type: Copy, dependsOn: subprojects.jar) {
    from(subprojects.jar)
    into 'build/dependencies/app'
}

task copyThirdPartyDependencies(type: Copy, dependsOn: subprojects.jar) {
    from(subprojects.configurations.runtimeClasspath)
    exclude(subprojects.jar*.archiveName)
    into 'build/dependencies/third-party'
}

build.dependsOn copyAppDependencies
build.dependsOn copyThirdPartyDependencies
